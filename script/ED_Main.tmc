
//----------------------------------------------------------------------//
//	CMDR Clicker's Elite Dangerous Thrustmaster Warthog + TFRP Script	//
//----------------------------------------------------------------------//

// This file:	ED_Main.tmc 		v5.0.0
// Supports:	Elite Dangerous 	v?.? (Odyssey)
// Requires:	TARGET 				v3.0.20.826
//				Joystick firmware	v12
//				Throttle firmware 	v23

// Refer Change Log.txt in same folder

// LEGEND: 
// ========================================================
// fnName()					refer to ED_Functions file
// initName()				refer to ED_Functions file
// fnsName()				refer to ED_StateTracker file
// tgName()					refer to ED_Toggles file
// mName					refer to ED_Macros file
// MapKey statements		refer to ED_MapKeyAssignment file
// Global Defines			refer to ED_Defines file
// Global Variables			refer to ED_GlobalVars file
// User Config Settings		refer to ED_UserSettings file
// ========================================================

// Index:	Line		FUNCTION
//			Line 39:	include statements
//			Line 60: 	int main()
//			Line 128:	int EventHandle()


// START

// -------------- //
// included files //
// -------------- //

include "target.tmh"

// 'target.tmh' include is mandatory.
// 		when loaded, target.tmh then includes "defines.tmh" and "hid.tmh"
// 		when loaded, hid.tmh then includes "sys.tmh"
//		...this ensures these critical files are loaded in the correct order
// The above .tmh files are bundled with the TARGET software.

include "ED_Defines.ttm"				// The order that these files are 'included' matters
include "ED_GlobalVars.tmh"
include "ED_UserSettings.tmh"
include "ED_Macros.tmh"
include "ED_MapKeyAssignment.tmh"
include "ED_Functions.tmh"
include "ED_StateTracker.tmh"
include "ED_Toggles.tmh"

// --------------- //
// program startup //
// --------------- //

	int main() {
	
		printf("\x0a\++	Exclude unwanted controllers	...	");					// Eliminates error messages relating to unfound devices
			
		Exclude(&HCougar);
		Exclude(&T16000);
		Exclude(&T16000L);
		Exclude(&JoystickF18);
		Exclude(&TWCSThrottle);
		Exclude(&TFRPHARudder);
		Exclude(&LMFD);
		Exclude(&RMFD);
		Exclude(&A320Pilot);
		Exclude(&A320Copilot);
		Exclude(&TCAQuadrant12);
		Exclude(&TCAQuadrant34);
		
		if (EnableTFRPRudder) {													// (see ED_UserSettings.tmh)
			Configure(&TFRPRudder, MODE_FILTERED);
		}
		else {
			Exclude(&TFRPRudder);
		}
		
		printf("done	++\x0a\x0a");
		printf("++	Connect our controllers ... \x0a\x0a");
		
		if(Init(&EventHandle)) return 1;
				
	// ---------------------------------------- //
	// Initialise LEDs, variables and functions //
	// ---------------------------------------- //
	
		printf("\x0a\++	Initialise hardware	...	Start	++\x0a"); 

		SetShiftButton(&Joystick, S3, &Throttle, PSF, PSB);						// ** DO NOT TOUCH **  Joystick Nosewheel Switch (S3) = IO, Throttle pinky switch (PSF/PSB) = UMD

		SetKBRate(PulseTime, DelayTime);										// Base Settings 	(See ED_UserSettings.tmh)
		SetKBLayout(KBLayout);													//					(See ED_Defines.ttm)
	
		fnCustomCommands();														// Initialise Custom Macros (See EDMacros.tmh)
		fnPIPMode(0);															// Reset PIPMode

		fnsMyStates(LOAD);														// Load my status variables
		
		fnLEDBrightness();														// Set initial LED Brightness levels
		initTextToSpeech();														// Initialise Text-To-Speech engine
		initSoundFX();															// Initialise Sound Effects engine
		

	// -------------------------- //
	// Initialise Flight controls //
	// -------------------------- //
		
		initJoystickAxis();
		initThrottleAxis();														// Includes Throttle "Friction Control" Slider
		initTFRPRudder();

		printf("\x0a\++	Initialise Hardware	...	Complete	++\x0a");
		printf("\x0a\++	Check Game state ...\x0a");
		
		fnsStartCheck();														// Check for vital files and game start
				
		if(Flags == 0) {
			printf("++	Waiting for Game to load...\x0a");	
			printf("\x0a\Script initialized and running @ %s \x0a", fnGetTOD());
		}

	}	// end of int main()
	

	int EventHandle(int type, alias o, int x) {
		DefaultMapping(&o, x);
	}

// END
