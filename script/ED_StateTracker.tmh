
//----------------------------------------------------------------------//
//	CMDR Clicker's Elite Dangerous Thrustmaster Warthog + TFRP Script	//
//----------------------------------------------------------------------//

// This file: ED_StateTracker.tmh (v5.0.0) ... supports Elite Dangerous (Odyssey)

// Refer Change Log.txt parent folder

// LEGEND: 
// ========================================================================
// fnName()								refer to ED_Functions file
// initName()							refer to ED_Initialise file
// stfnName()							refer to ED_StateTracker file
// tgName()								refer to ED_Toggles file
// mName								refer to ED_Macros file
// MapKey statements					refer to ED_MapKeyAssignment file
// Global State and Boolean Defines		refer to ED_ScriptDefines file
// Global Game Key Bind Defines			refer to ED_GameBindings file 
// Global Variables						refer to ED_GlobalVars file
// User Config Settings					refer to ED_UserSettings file
// ========================================================================

// Index:	Line		FUNCTION
//			Line 47: 	int stfnReadStatusJson()
//			Line 135: 	int stfnGetKeyValue()
//			Line 247: 	int stfnProcessFlags()
//			Line 484:	int stfnProcessFlags2()
//			Line 548: 	int stfnProcessGuiFocus()
//			Line 582: 	int stfnStartCheck()
//			Line 626:	int stfnMyStates()
//			Line 700:	int stfnDumpFlags()
	
// START


	// FUNCTION:	Read status.json into 'myJson'
	// Requires:	'StatusFile' string variable to be correctly set in ED_UserSettings file

	// GlobalVars

	char	myJson;	
	int		ReadErrorCount	= 0;
	
	// ------------
	
	int stfnReadStatusJson() {

		int 	VoiceTxt;
		int 	fp;
		int 	buf = 512;
		int		myResult;
		int		ReadErrorSent	= 0;
		int		newJson			= 0;								// set when myJson != lastJson...we then call stfnProcessFlags();		

		Dim(&myJson, buf);
		myJson[0] = NUL;
		
		char	lastJson;
 		Dim(&lastJson, buf); 
 		lastJson[0] = NUL;		
		
		fp = fopen(&StatusFile, "r");
		
		if (fp) {
			myResult = fread(&myJson, 1, buf, fp);
			fclose(fp);
			if (myResult == 0) {
				ReadErrorCount = ReadErrorCount + 1;
				if (ReadErrorCount > 3) {
					printf("File Read Error Count = %d : status.json\x0a", ReadErrorCount);
					if (!ReadErrorSent) {
						VoiceTxt = "Status Jason Read Error count exceeded";
						fnTextToSpeech(VoiceTxt, 100);
						ReadErrorSent = 1;
					}
				}
			}
			else {
				ReadErrorCount 		= 0;
				ReadErrorSent 		= 0;
				myJson[myResult] 	= 0;								// terminates char array with nul
				
				newJson = strcmp(&myJson, &lastJson);
				if (newJson != 0) {					
					Flags = stfnGetKeyValue("Flags", INTEGER);
					if (OldFlags != Flags)	{							// Process 'Flags' only when it changes
						stfnProcessFlags(0);
					}
					OldFlags = Flags;
					
					if (Odyssey) {
						Flags2 = stfnGetKeyValue("Flags2", INTEGER);
						if (Flags2 == -1) {
							Odyssey = 0;
						}
						else {
							if (OldFlags2 != Flags2) {					// Process 'Flags2' only when it changes
								stfnProcessFlags2();
							}
							OldFlags2 = Flags2;
						}
					}
				}			
				sprintf(&lastJson, "%s", &myJson);
				lastJson[strlen(&lastJson)] = NUL;						//terminate char array 
			}
		}
		else {															// ED must have the file locked for reading?
			printf("ERROR: Status.jason file not found\x0a");			// trap if status.json is suddenly unavailable during game
			fnSoundFX("wstop");
		}		
	}


	// FUNCTION:	Extract json key value by name from status.json
	// Input:		Key name (pattern: string), key value type (vtype: FLOAT, INTEGER, STRING)
	// Output:		Returns key value
	
	// Valid Key Names:	pattern(vtype) for HORIZONS
	//				Flags(INTEGER), Pips(STRING), Firegroup(INTEGER), GuiFocus(INTEGER), FuelMain(FLOAT), FuelReservoir(FLOAT)
	//				Cargo(INTEGER), LegalState(STRING), Latitude(FLOAT), Longitude(FLOAT), Altitude(INTEGER), Heading(INTEGER)
	//				BodyName(STRING), PlanetRadius(INTEGER), timestamp(STRING), event(STRING)

	//					pattern(vtype) for additional Keys in ODYSSEY
	//				Flags2(INTEGER), when on foot, the following will be available;
	//				Oxygen(FLOAT), Health(FLOAT), Temperature(INTEGER), SelectedWeapon(STRING), Gravity(FLOAT)

	// GlobalVars

	char	keyvalue;	

	// ----------

	int stfnGetKeyValue(alias pattern, int vtype) {
		
		int 	PatLen = strlen(&pattern);
				
		int 	valstart	= 0;									// string position of where the key value starts
		int 	vallen		= 0;									// string length of the key value
		int 	chrpnt		= 0;									// character pointer used to determine where key value ends
		int		EndValue	= 0;									// flag to set when we hit the end of value
		
		char 	thischr;											// char variable to detect end of value via a ',' or '}' or ' '	
		Dim(&thischr, 2);
		thischr[0]	= NUL;
		
		char	myStr;
		Dim(&myStr, PatLen);
		myStr[0] 	= NUL;

		Dim(&keyvalue, 64);											// keyvalue char is global (used in status dump)
		keyvalue[0] = NUL;

		float	fkeyvalue;											// floating point key values
		int		ikeyvalue;											// integer key value
		int		PseudoText = "";
		int		myResult;
		
		// Find the Key (pattern) ...

		int Start = findstr(&myJson, &pattern);						// 'findstr()' is my version of 'strstr()' in sys.tmh...which doesn't work...defined in ED_Functions.tmh 
																	// 'Start' contains the character count to the first character of the Keyname, if found. 
		if (Start == -1) {											// 'Start' contains -1 if the Keyname is not found
			myResult = strcmp(&pattern, "Flags2");
			if (myResult != 0) {									// Ignore for Flags2 until Horizons is defunct, then remove this test.
				printf("Key %s NOT Found\x0a", &pattern);
			}
			return -1;
		}
		
		// Now grab the key-value...
		
		valstart = Start + PatLen + 2;								// start of value we're after

		chrpnt = 0;
						
		myResult = strcmp(&pattern, "Pips");
		
		if (myResult == 0) {										// pattern = "Pips"
			vallen = 5;
			while(chrpnt < vallen) {
				strsub(&thischr, &myJson, valstart+chrpnt, valstart+chrpnt);
				strcat(&keyvalue, &thischr);
				chrpnt = chrpnt + 1;
			}
		}
		else {
			strsub(&thischr, &myJson, valstart, valstart);
			myResult = strcmp(&thischr, ",");
			if (myResult == 0 & (vtype == FLOAT | vtype == INTEGER)) EndValue = 1;
		
			myResult = strcmp(&thischr, "}");
			if (myResult == 0) EndValue = 1;
			
			myResult = strcmp(&thischr, " ");
			if (myResult == 0 & (vtype == FLOAT | vtype == INTEGER)) EndValue = 1;
			
			if (EndValue == 0) strcat(&keyvalue, &thischr);
			
			while (EndValue == 0 & chrpnt < 20) {
				chrpnt = chrpnt + 1;
				strsub(&thischr, &myJson, valstart+chrpnt, valstart+chrpnt);
				
				myResult = strcmp(&thischr, ",");					// end of value signalled by ',' or '}' or ' ' or '"' 
				if (myResult == 0 & (vtype == FLOAT | vtype == INTEGER)) EndValue = 1;
				
				myResult = strcmp(&thischr, "}");
				if (myResult == 0) {
					EndValue = 1;
					chrpnt = chrpnt - 1;
				}
				
				myResult = strcmp(&thischr, " ");
				if (myResult == 0 & (vtype == FLOAT | vtype == INTEGER)) EndValue = 1;

				myResult = strcmp(&thischr, "\"");
				if (myResult == 0 & vtype == STRING) EndValue = 1;			
				
				if (EndValue == 0) strcat(&keyvalue, &thischr);		// continue building char array
			}			
		}
		
		keyvalue[chrpnt] = NUL;										// terminate char array with a nul 
				
		// Return converted key-value...
		
		if (vtype == FLOAT) {											// convert string to float
			return eval(&keyvalue);
		}
		if (vtype == INTEGER) {											// convert string to integer
			return ieval(&keyvalue);
		}

		if (vtype == STRING) {			
			strdel(&keyvalue, 0, 0);								// Just remove leading quote and...
			return 0;												// ...use the global keyvalue on return
		}
	}
	// End of stfnGetKeyValue()


	// FUNCTION: Read and process "Flags" value from "status.json"
	// Input: 'UpdateLEDs', 1 = update, 0 = no update (default)
	// Output: Flags variables set/reset

	int stfnProcessFlags(int UpdateLEDs = NO) {

		int VoiceTxt;

		// These 2-letter shorthand duplicate variables enable me to track when certain status bits change.
		// Set these first before re-decoding latest flags key value
		
		int ll = fLatLong;												// Track in/out of orbit (lower case LL)
		int sc = fSupercruise;											// Track Supercruise changes
		int wd = fsdJump;												// Track when we jump systems
		int sh = fMainShip;												// Track in/out of MainShip
		int dk = fDocked;												// Track when we dock in station or star port
		int ld = fLanded;												// Track when landed
		int oh = fOverHeating;											// Track when we overheat
		int su = fShieldsUp;											// Track when shields drop
		int dg = fDanger;												// Track when we are in danger
		int tr = fSRVTurretRetracted;									// Track when SRV Turret is retracted
		
		int fa = fFAOff;												// LED 1 Tracking
		int lt = fLights;												// LED 2 Tracking
		int sr = fSilentRunning;										// LED 3 Tracking
		int cs = fCargoScoop;											// LED 4 Tracking
		int lg = fLandingGear;											// LED 5 Tracking
				
		if (strlen(&myJson) == 68) {									// When Game is halted, string length of status.json = 68 chars 
			if(GameLoaded == YES) {										// Detects when Game halts
				GameLoaded = NO;
				Odyssey = 0;											// Turn off Flags2 processing

				VoiceTxt = "Game halted -";
				if(TrainingMode == ENABLED){
					TrainingMap();										// Load KeyMap profile for training
					if (AnnounceTraining) {
						VoiceTxt = "Game halted. Training mode enabled -";
					}
				}
				
				printf("\x0a- ");
				fnVPOutput(VoiceTxt);
				
				if (TrainingMode == ENABLED & AnnounceTraining == DISABLED) {
					printf("Training Mode enabled\x0a");
				}
				
				if (ReverseThrust) {
					ReverseThrust = !ReverseThrust;
				}
				
				fnSetLEDs();															// Turn off Status LEDs, then...
				ActKey(PULSE+KEYON+LED(&Throttle, LED_ONOFF, LED_CURRENT-LED0)); 		// set GLOBAL LED OFF

				stfnMyStates(SAVE);
			}
		}
		else {		// strlen(&myJson) > 68								// Game is running 

		// TARGET uses 32 bit signed precision for its integers.
		// Integers larger than 2,147,483,648 wrap around to negative number
		// Note: "bitwise and" (&) test won't work for a negative number
		// and can only mean bit 31 (fSRVHiBeam) is set.

		// Process 'Flags' value...most significant bit order
		
			if (Flags < 0) {											// Most significant bit is set 
				fSRVHiBeam			= ON;
			}
			else {
				fSRVHiBeam			= OFF;
			}
			
			// Bitwise-And of the negative number works for all bits other than most significant bit 
					
			fsdJump				= ((Flags & ED_FSDJump) > 0);
			fAltFromAvgRad		= ((Flags & ED_AltFromAvgRad) > 0);
			fNightVision		= ((Flags & ED_NightVision) > 0);
			fHUDAnalysis		= ((Flags & ED_HudInAnalysisMode) > 0);
			fSRV				= ((Flags & ED_InSRV) > 0);
			fFighter			= ((Flags & ED_InFighter) > 0);
			fMainShip			= ((Flags & ED_InMainShip) > 0);
			fInterdicted		= ((Flags & ED_BeingInterdicted) > 0);
			fDanger				= ((Flags & ED_IsInDanger) > 0);
			fLatLong			= ((Flags & ED_HasLatLong) > 0);
			fOverHeating		= ((Flags & ED_OverHeating) > 0);
			fLowFuel			= ((Flags & ED_LowFuel) > 0);
			fFSDCooldown		= ((Flags & ED_FSDCooldown) > 0);
			fFSDCharging		= ((Flags & ED_FSDCharging) > 0);
			fMassLocked			= ((Flags & ED_FSDMassLocked) > 0);
			fSRVDriveAssist		= ((Flags & ED_SRVDriveAssist) > 0);
			fSRVTurretRetracted	= ((Flags & ED_SRVTurretRetracted) > 0);
			fSRVTurretView		= ((Flags & ED_SRVTurret) > 0);
			fSRVHandbrake		= ((Flags & ED_SRVHandbrake) > 0);
			fFuelScoop			= ((Flags & ED_ScoopingFuel) > 0);
			fSilentRunning		= ((Flags & ED_SilentRunning) > 0);
			fCargoScoop			= ((Flags & ED_CargoScoopDeployed) > 0);
			fLights				= ((Flags & ED_LightsOn) > 0);
			fInWing				= ((Flags & ED_InWing) > 0);
			fHardpoints			= ((Flags & ED_HardpointsDeployed) > 0);
			fFAOff				= ((Flags & ED_FlightAssistOff) > 0);
			fSupercruise		= ((Flags & ED_Supercruise) > 0);
			fShieldsUp			= ((Flags & ED_ShieldsUp) > 0);
			fLandingGear		= ((Flags & ED_LandingGearDown) > 0);
			fLanded				= ((Flags & ED_Landed) > 0);
			fDocked				= ((Flags & ED_Docked) > 0);

			if (fLatLong != ll) {										// ie lower case LL. Detect when we cross the Orbital Cruise boundary
				OrbitalCruise = !OrbitalCruise;			
			}

			if (fSupercruise != sc) {									// Detect when Supercruise toggles
				fnGetFlightMode();										// Line up slider curves
				if (!fSupercruise) {
					if (SelectDrop == NO) {								// Uninitiated drop
						
						if (OrbitalCruise & !fInterdicted) {
							VoiceTxt = "Entering Glide Mode\x0a";
						}
						else { 
							VoiceTxt = "Drop detected\x0a";
						}
						
						fnVPOutput(VoiceTxt);							//  Announce drop type
						
						if (fInterdicted | fDanger) {
							printf("!!! DANGER DETECTED !!!\x0a");
							if (fHUDAnalysis) {
								tgHUDMode();							// Select Combat HUD
							}
							if (!fHardpoints) {
								tgHardpoints();							// Deploy weapons
							}
							DeferCall(1500, &ActKey, PULSE+KEYON+SelectHighestThreat);					// Select shooter
							
							fnTextToSpeech("Warning! Danger! Combat Mode Initiated", 100);				// Sound warning 
						}
					}
					else {												// Initiated drop
						SelectDrop  = NO;
					}
					UpdateLEDs = YES;
				}
			}

			if (fsdJump != wd) {										// Detect when Supercruise or Hyperjump has been toggled
				if (fsdJump) {
					if (!fHUDAnalysis & AutoHUD) {
						tgHUDMode();		
					}
				}
			}
			
			if (!fShieldsUp & su & !fSilentRunning) {					// Shields just dropped
				if (fDanger) {
					if (AutoStrobe) {
						DeferCall(750, &fnVPOutput, "Activating Silent Strobe\x0a");
						tgSilentRunning(STROBE);
					}
					else {
						DeferCall(750, &fnTextToSpeech, "Consider using Silent Running commander");
					}
				}
			}

			if (fMainShip & (fLanded != ld)) {							// Framework for future functionality
							
				if (fLanded) {
					printf("Landed @ %s\x0a", fnGetTOD());
				}
				else {
					printf("Lift off @ %s\x0a", fnGetTOD());
					if (AutoGearUP) {
						ActKey(PULSE+KEYON+LandingGear);
					}
					ShipDismissed = NO;			
				}
			}
			
			if (fMainShip & (fDocked != dk)) {		// Framework - currently testing - may not be viable in current form without additional information from journal
							
				if (fDocked) {							
					if (AutoHanger) {
						DeferCall(2500, &fnHangerServices, 1);			// Cannot use 'Auto' feature until we can determine if in a Starport v Carrier
					}
					DeferCall(1000, &ActKey, PULSE+KEYON+BalancePower);
					printf("Docked @ %s\x0a", fnGetTOD());
				}
				else	{	
					InHanger = NO;
					if (AutoGearUP) {
						DeferCall(1500, &ActKey, PULSE+KEYON+LandingGear);
					}
					printf("Launch @ %s\x0a", fnGetTOD());					
				}
			}
			
			if (fOverHeating & !oh) {									// Detect when we start overheation
				if (AutoHeatsink) {										// If 'AutoHeatSink' is set, fire a heatsink if we have one
					fnVPOutput("Auto Heat sink engaged!");
					ActKey(PULSE+KEYON+DeployHeatSink);
				}
				else {
					fnTextToSpeech("Consider firing a heat sink commander", 100);
				}
			}
			
			if (fSRV & (fSRVTurretRetracted != tr)) {					// Detect when the turret deploys or retracts
				if (fSRVTurretRetracted) {
					fnTextToSpeech("Turret Retracted");
				}
				else {
					fnTextToSpeech("Turret Deployed");
				}
			}

			// Detect if we need to update the LEDs						
			if (fa != fFAOff | lt != fLights | sr != fSilentRunning | cs != fCargoScoop | lg != fLandingGear | sh != fMainShip) UpdateLEDs = YES;

			if(GameLoaded == NO) {										// Game started...as opposed to re-started.
				fnGameStarted();
			}
			else if(UpdateLEDs == YES) {
				fnSetLEDs();
			}
			
			OldFlags = Flags;											// Keep track of last Flags Value to track when Flag bits change
			
			if (ShowFlags) {											// Printout active flag bits when 'ShowFlags' is set 
				stfnDumpFlags();
			}
		}
	}
	
	// END of stfnProcessFlags()


	// FUNCTION:	Read and process "Flags2" value from "status.json"
	// Output:		Flags2 variables set/reset
	
	int stfnProcessFlags2() {
	
		int VoiceTxt;

		// TARGET uses 32 bit signed precision for its integers.
		// Integers larger than 2,147,483,648 wrap around to negative number
		// Note: "bitwise and" (&) test won't work for a negative number
		// and can only mean bit 31 (fSRVHiBeam) is set.

		// Process 'Flags2' value...most significant bit order
		
//		if (Flags2 < 0) {											// Most significant bit is set 
//			fRESERVED			= ON;
//		}
//		else {
//			fRESERVED			= OFF;
//		}
			
		// Bitwise-And of the negative number works for all bits other than most significant bit 
					
		fOnFoot					= ((Flags2 & ED_OnFoot) > 0);					// 'ED_...' masks defined within 'ED_ScriptDefines' file 
		fInTaxi					= ((Flags2 & ED_InTaxi) > 0);
		fInMultiCrew			= ((Flags2 & ED_InMultiCrew) > 0);
		fOnFootInStation		= ((Flags2 & ED_OnFootInStation) > 0);
		fOnFootOnPlanet			= ((Flags2 & ED_OnFootOnPlanet) > 0);
		fAimDownSight			= ((Flags2 & ED_AimDownSight) > 0);
		fLowOxygen				= ((Flags2 & ED_LowOxygen) > 0);
		fLowHealth				= ((Flags2 & ED_LowHealth) > 0);
		fCold					= ((Flags2 & ED_Cold) > 0);
		fHot					= ((Flags2 & ED_Hot) > 0);
		fVeryCold				= ((Flags2 & ED_VeryCold) > 0);
		fVeryHot				= ((Flags2 & ED_VeryHot) > 0);
		fGlideMode				= ((Flags2 & ED_GlideMode) > 0);
		fOnFootInHanger			= ((Flags2 & ED_OnFootInHanger) > 0);
		fOnFootSocialSpace		= ((Flags2 & ED_OnFootSocialSpace) > 0);
		fOnFootExterior			= ((Flags2 & ED_OnFootExterior) > 0);
		fBreathableAtmosphere	= ((Flags2 & ED_BreathableAtmosphere) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);
//		fRESERVED				= ((Flags2 & ED_RESERVED) > 0);

		OldFlags2 = Flags2;											// Keep track of last Flags2 Value to track when Flags2 bits change
			
		if (ShowFlags) {											// Printout active flag bits when 'ShowFlags' is set 
			stffnDumpFlags();
		}				
	}
	
	//END of stfnProcessFlags2()
	
	
	// FUNCTION: Read and process 'GuiFocus' key value in status.json

	int stfnProcessGuiFocus() {
		int GUIFocus;

		if (Flags != 0) {
			GUIFocus = stfnGetKeyValue("GuiFocus", INTEGER);
			if (GUIFocus == GUI_FSSMode) {
				FSSMode = ON;
			}
			else FSSMode = OFF;
			
			if (GUIFocus == GUI_SAAMode) {
				SAAMode = ON;
			}
			else SAAMode = OFF;
			
			return GUIFocus;
		}
		else {
			return -1;
		}
	}


	// FUNCTION: Initial check to see if game is already running after we've restarted the script.
	
	int stfnStartCheck() {
		int fp;								// file pointer (handle)
		
		fp = fopen(&StatusFile, "r");
		if (fp) {
			fclose(fp);
			printf("\x0a\status.json file set correctly in ED_UserSettings");
		}
		else {
			printf("\x0a\x0a\!!! FATAL ERROR !!!\x0a\x0a");
			printf("status.json file not found.\x0a\x0a");
			printf("Please set 'StatusFile' alias correctly in ED_UserSettings file\x0a");
			printf("Refer ED_UserSettings.tmh file for instructions.\x0a");
			Abort();
		}
		
		if (StartupLock == ON) {
			printf("\x0a\status.json location set to %s\x0a\x0a", &StatusFile);			
		}
		
		stfnReadStatusJson();																	// Read status.json first time before starting loop. Won't work otherwise. I don't know why.
		ActKey(KEYON+REXEC(hReadStatusJson, StatusReadRate, "stfnReadStatusJson();", RNOSTOP));	// StatusReadRate is set in UserSettings file

		if (strlen(&myJson) == 68) fnSoundFX("tada");		
	}

	// FUNCTION:	Save states not included in status.json
	// PURPOSE:		Save current status of certain variables when mode switching or restarting the game
	// REQUIRES:	'MyStatusFile' path to be set correctly within ED_UserSettings.tmh file
	// PARAMETERS:	action = SAVE or LOAD

	// Global declarations
	
	struct MyStruct {
		int		sVolume;
		int 	sDismissed;
		int 	sCurves;
		int 	sLEDLevel;
	}
	
	MyStruct MyStates;
	
	// -------------------

	int stfnMyStates(int action)	{

		int fnName = "MyStates";
		int reason;
		int fh;															// General File Pointer Variable 
		int fpresult;

		if (action == SAVE)	{
						
			// Build our struct array
			MyStates.sVolume	=	VoiceVolume;
			MyStates.sDismissed =	ShipDismissed;
			MyStates.sCurves	=	OldCurves;
			MyStates.sLEDLevel	=	LEDLevel;
			
			fh = fopen(&MyStatusFile, "w");								// creates the file if it doesn't exist
			
			if (fh) {
			
				fpresult = fwrite(&MyStates, sizeof(&MyStates), 1, fh);				
				fclose(fh);
				
				if (fpresult == -1) {
					fnVPOutput("Error writing MyStates to status file", 100);
				}	
				else {
					printf("MyStates successfully saved @ %s\x0a", fnGetTOD());
				}
			}
			else {
				fnTextToSpeech("Error writing to My Status File");
				printf("Error opening file to write @ %s: %s\x0a", fnGetTOD(), &MyStatusFile);
			}		
		}
		
		else if (action == LOAD) {

			fh = fopen(&MyStatusFile, "r");								// open file to read
			
			if (fh) {

				fpresult = fread(&MyStates, sizeof(&MyStates), 1, fh);	// Load MyStates from file				
				
				if (fpresult > 0) {	// Read from file was successful
					fclose(fh);							

					// Extract our variables from our struct array
					VoiceVolume		= MyStates.sVolume;
					ShipDismissed	= MyStates.sDismissed;
					OldCurves		= MyStates.sCurves;
					LEDLevel		= MyStates.sLEDLevel;
					
					printf("\x0a\MyStates successfully loaded @ %s\x0a\x0a", fnGetTOD());
				}
				else {
					fnVPOutput("Error reading MyStates from status file", 100);
				}
			}
			else {
				fnTextToSpeech("Non fatal error, my Status File not found or invalid, check con sole message"); 
				printf("\x0a\!!! Non-Fatal Error opening file to read: %s\x0a", &MyStatusFile);
				printf("This can happen if the file does not exist or is missing array elements\x0a");
			}
		}
		
		else {
			reason = "action not valid";
			fnNotValid(fnName, reason);
			printf("stfnMyStates Action = %d\x0a", action);
		}
	}

	// FUNCTION:	Dump currently set Flags and Flags2 Key Values to console
	
	int stfnDumpFlags() {

		int GUIFocus;
		int prttxt;
		
		GUIFocus = stfnGetKeyValue("GuiFocus", INTEGER);
		
		if (GUIFocus == GUI_None)			prttxt = "No Focus";
		if (GUIFocus == GUI_SystemPanel)	prttxt = "System Panel";
		if (GUIFocus == GUI_TargetPanel)	prttxt = "Target Panel";
		if (GUIFocus == GUI_CommsPanel)		prttxt = "Comms Panel";
		if (GUIFocus == GUI_RolePanel)		prttxt = "Role Panel";
		if (GUIFocus == GUI_StationSvcs)	prttxt = "Station Services";
		if (GUIFocus == GUI_GalaxyMap)		prttxt = "Galaxy Map";
		if (GUIFocus == GUI_SystemMap)		prttxt = "System Map";
		if (GUIFocus == GUI_OrreryMap)		prttxt = "Orrery Map";
		if (GUIFocus == GUI_FSSMode) 		prttxt = "FSS Mode";		
		if (GUIFocus == GUI_SAAMode) 		prttxt = "SAA Mode";		
		if (GUIFocus == GUI_Codex)			prttxt = "Codex";
		if (GUIFocus == -1)					prttxt = "Flags = 0, No GUIFocus Key?";
		
		printf("\x0a\GUI Focus = %s\x0a", prttxt);
		
		printf("\x0a\Flags	=	%d\x0a", Flags);
		printf("=========================\x0a");
		printf("Bits set in FLAGS:\x0a");				
		if (fSRVHiBeam) 			printf("SRVHiBeam\x0a");
		if (fsdJump) 				printf("Hyperjump\x0a");
		if (fAltFromAvgRad) 		printf("Radius-ALT\x0a");
		if (fNightVision) 			printf("Night Vision\x0a");
		if (fHUDAnalysis) 			printf("AnalysisHUD\x0a");
		if (fSRV) 					printf("In SRV\x0a");
		if (fFighter) 				printf("In Fighter\x0a");
		if (fMainShip) 				printf("In MainShip\x0a");
		if (fInterdicted) 			printf("Being Interdicted\x0a");
		if (fDanger) 				printf("!!! DANGER !!!\x0a");
		if (fLatLong) 				printf("Lat\\Lon available\x0a");
		if (fOverHeating) 			printf("Over Heating\x0a");
		if (fLowFuel) 				printf("Low Fuel\x0a");
		if (fFSDCooldown) 			printf("FSD Cooldown\x0a");
		if (fFSDCharging) 			printf("FSD Charging\x0a");
		if (fMassLocked) 			printf("FSD Masslocked\x0a");
		if (fSRVDriveAssist) 		printf("SRV Drive Assist\x0a");
		if (fSRVTurretRetracted) 	printf("SRV Turret Retracted\x0a");
		if (fSRVTurretView) 		printf("SRV Turret View\x0a");
		if (fSRVHandbrake) 			printf("SRV Handbrake\x0a");
		if (fFuelScoop) 			printf("Fuel Scoop Deployed\x0a");
		if (fSilentRunning) 		printf("Silent Running\x0a");
		if (fCargoScoop) 			printf("Cargo Scoop Deployed\x0a");
		if (fLights) 				printf("Lights On\x0a");
		if (fInWing) 				printf("In Wing\x0a");
		if (fHardpoints) 			printf("Hardpoints Deployed\x0a");
		if (fFAOff) 				printf("Flight Assist OFF\x0a");
		if (fSupercruise) 			printf("Supercruise Engaged\x0a");
		if (fShieldsUp) 			printf("Shields Up\x0a");
		if (fLandingGear) 			printf("Landing Gear Deployed\x0a");
		if (fLanded) 				printf("Landed on planet\x0a");
		if (fDocked) 				printf("Docked\x0a");
		printf("=========================\x0a");

		if (Odyssey) {		
			printf("\x0a\Flags2	=	%d\x0a", Flags2);
			printf("=========================\x0a");
			printf("Bits set in FLAGS2:\X0A");
			if (fOnFoot)	 			printf("On Foot\x0a");
			if (fInTaxi) 				printf("In Taxi\x0a");
			if (fInMultiCrew) 			printf("In Multi Crew\x0a");
			if (fOnFootInStation) 		printf("On Foot In Station\x0a");
			if (fOnFootOnPlanet) 		printf("On Foot On Planet\x0a");
			if (fAimDownSight) 			printf("Aim Down Sight\x0a");
			if (fLowOxygen) 			printf("Low Oxygen\x0a");
			if (fLowHealth) 			printf("Low Health\x0a");
			if (fCold) 					printf("Cold\x0a");
			if (fHot) 					printf("Hot\x0a");
			if (fVeryCold) 				printf("Very Cold\x0a");
			if (fVeryHot) 				printf("Very Hot\x0a");
			if (fGlideMode)				printf("Glide Mode\x0a");
			if (fOnFootInHanger)		printf("On Foot In Hanger\x0a");
			if (fOnFootSocialSpace)		printf("On Foot in Social Space\x0a");
			if (fOnFootExterior)		printf("On Foot Exterior\x0a");
			if (fBreathableAtmosphere)	printf("Breathable Atmosphere\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
//			if (fRESERVED) 				printf("RESERVED\x0a");
			printf("=========================\x0a");
		}	
	}

// END
